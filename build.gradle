// alias: gets plugin id and version from version catalog
plugins {
    id "java"
    alias(libs.plugins.osdetector)
    alias(libs.plugins.javafx)
}

subprojects {
    apply plugin: "java"
    pluginManager.apply(libs.plugins.javafx.get().getPluginId())
    pluginManager.apply(libs.plugins.osdetector.get().getPluginId())
    repositories {
        mavenCentral()
    }
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(libs.versions.java.get())
        }
    }
    javafx {
        version = libs.versions.javafx.get()
    }
    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
        options.compilerArgs.addAll(["-Xlint:deprecation", "-Xlint:unchecked"])
    }
}

ext {
    osPkg = osdetector.os == "windows" ? "msi"
          : osdetector.os == "mac" ? "dmg"
          : osdetector.os == "linux" ? "deb"
          : ""
}

println "osdetector.os         = ${osdetector.os}"
println "osdetector.arch       = ${osdetector.arch}"
println "osdetector.classifier = ${osdetector.classifier}"