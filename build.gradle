plugins {
    id "org.openjfx.javafxplugin" apply false
}

subprojects {
    apply(plugin: "java")
    apply(plugin: "org.openjfx.javafxplugin")
    repositories {
        mavenCentral()
    }
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(libs.versions.java.get())
        }
    }
    javafx {
        version = libs.versions.javafx.get()
    }
    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
        configure(options) {
            options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
        }
    }
}

// OS detection
def osNameSystemProperty = System.getProperty("os.name")
ext {
    osName  = osNameSystemProperty.contains("Windows") ? "windows"
            : osNameSystemProperty.contains("Mac")     ? "macosx"
            : osNameSystemProperty.contains("Linux")   ? "linux"
            : "unidentifiedOS"
    osPkg   = osName == "windows" ? "msi"
            : osName == "macosx"  ? "dmg"
            : osName == "linux"   ? "deb"
            : ""
}
println("System property 'os.name'='$osNameSystemProperty'. Using 'ext.osName'='$osName' and 'ext.osPkg'='$osPkg' in build files.")
