plugins {
    id "java"
    id "org.beryx.jlink" version "3.1.1"
}

dependencies {
    implementation project(":pacman-core")
    implementation project(":pacman-ui-lib")
    implementation project(":pacman-ui")
    implementation project(":pacman-app-arcade-pacman")
    implementation project(":pacman-app-arcade-pacmanxxl")
    implementation project(":pacman-app-arcade-mspacman")
    implementation project(":pacman-app-tengen-mspacman")
    implementation libs.bundles.tinylog
}

application {
    // Do NOT ask me what is going on here! Without the "x." prefix the exe does not work
    // and an error message "module de.amr.pacmanfx.allgames not in boot Layer" appears
    mainModule = "x.de.amr.pacmanfx.allgames"
    mainClass = "de.amr.pacmanfx.allgames.app.Main"
    if (osName == "linux") {
        applicationDefaultJvmArgs = ["-Dprism.forceGPU=true"]
    }
}

javafx {
    modules = ["javafx.base", "javafx.graphics", "javafx.controls", "javafx.media"]
}

jlink {
    options = ["--compress", "zip-9", "--strip-debug", "--no-header-files", "--no-man-pages"]
    launcher {
        name = "Pac-Man-FX-3D"
    }
    jpackage {
        appVersion = "1.0"
        vendor = "Armin Reichert"
        installerOptions = []
        installerType = osPkg
        if (osName == "windows") {
            icon = "pacman.ico"
            installerOptions += ["--win-dir-chooser", "--win-menu", "--win-menu-group", "Pac-Man FX", "--win-shortcut"]
            //imageOptions = ["--win-console"]
        }
    }
}
