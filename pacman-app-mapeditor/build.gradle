plugins {
    alias(libs.plugins.javafx)
    alias(libs.plugins.jlink)
    alias(libs.plugins.osdetector)
}

dependencies {
    implementation(
        project(":pacman-core"),
        project(":pacman-ui-lib"),
        libs.bundles.tinylog
    )
}

application {
    mainModule = "de.amr.pacmanfx.mapeditor"
    mainClass  = "de.amr.pacmanfx.mapeditor.app.Main"
}

javafx {
    modules = ["javafx.base", "javafx.graphics", "javafx.controls"]
}

jlink {
    options = ["--strip-debug", "--compress", "zip-9", "--no-header-files", "--no-man-pages"]
    launcher {
        name = "Pac-Man-FX-Map-Editor"
    }
    jpackage {
        vendor = "Armin Reichert"
        installerType = installerPkg
        if (osdetector.os == "windows") {
            icon = "mapeditor.ico"
            installerOptions = ["--win-dir-chooser", "--win-menu", "--win-menu-group", "Pac-Man FX", "--win-shortcut"]
        }
    }
}
