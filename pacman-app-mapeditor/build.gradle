plugins {
    id "application"
    id "java"
    id "org.beryx.jlink" version "3.0.1"
    id "org.openjfx.javafxplugin" version "0.1.0"
}

dependencies {
    implementation project(":pacman-core")
    implementation project(":pacman-ui-lib")
    implementation libs.bundles.tinylog
}

application {
    mainModule = "de.amr.games.pacman.tilemap.editor.app"
    mainClass  = "de.amr.games.pacman.tilemap.editor.app.Main"
}

javafx {
    modules = ["javafx.base", "javafx.graphics", "javafx.controls"]
}

jlink {
    imageZip = layout.buildDirectory.file("distributions/javafx-mapeditor-app-${javafx.platform.classifier}.zip")
    options = ["--strip-debug", "--compress", "zip-9", "--no-header-files", "--no-man-pages"]
    launcher {
        name = "Pac-Man-FX-Map-Editor"
    }
    jpackage {
        appVersion = "1.0"
        vendor = "Armin Reichert"
        if (os.isWindows()) {
            icon = "mapeditor.ico"
            installerType = "msi"
            installerOptions = ["--win-dir-chooser", "--win-menu", "--win-menu-group", "Pac-Man FX", "--win-shortcut"]
        }
        if (os.isLinux()) {
            installerType = "deb"
        }
    }
}

jlinkZip {
    group = "distribution"
}
