plugins {
    id 'java'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.jlink' version '3.0.1'
}

application {
    mainModule = 'de.amr.games.pacman.mapeditor'
    mainClass = 'de.amr.games.pacman.mapeditor.Main'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

dependencies {
    implementation project(':pacman-lib')
    implementation libs.tinylog_api
    implementation libs.tinylog_impl
}

javafx {
    version = versions.javafx
    modules = ['javafx.base', 'javafx.graphics', 'javafx.controls']
}

jlink {
    imageZip = layout.buildDirectory.file("distributions/javafx-mapeditor-${javafx.platform.classifier}.zip")
    options = [
            '--strip-debug',
            '--compress', 'zip-9',
            '--no-header-files',
            '--no-man-pages']
    launcher {
        name = 'Pac-Man-FX-Map-Editor'
    }
    jpackage {
        appVersion = '1.0'
        vendor = 'Armin Reichert'
        if (os.isWindows()) {
            icon = 'mapeditor.ico'
            installerType = 'msi'
            installerOptions = [
                    '--win-dir-chooser',
                    '--win-menu',
                    '--win-menu-group', 'Pac-Man FX',
                    '--win-shortcut']
        }
        if (os.isLinux()) {
            installerType = 'deb'
        }
    }
}

jlinkZip {
    group = 'distribution'
}
