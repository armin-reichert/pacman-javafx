plugins {
    id 'java'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.jlink' version '3.0.1' //  adds also 'application' plugin
}

group 'de.amr.games.pacman'
version '1.0'

ext.arch = System.getProperty("os.arch")
ext.os = org.gradle.internal.os.OperatingSystem.current()
println "Operating System: name='${os.name}' version='${os.version}' arch='${arch}'"

application {
    mainModule = 'de.amr.games.pacman.mapeditor'
    mainClass = 'de.amr.games.pacman.mapeditor.Launch'
}

dependencies {
    implementation project(':pacman-core')
    implementation project(':pacman-ui-fx-2d')
    implementation 'org.tinylog:tinylog-api:2.7.0'
    implementation 'org.tinylog:tinylog-impl:2.7.0'
}

javafx {
    version = '21.0.2'
    modules = ['javafx.controls', 'javafx.media']
}

repositories {
    mavenCentral()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

jlink {
    imageZip = project.file("${layout.buildDirectory}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'pacman-javafx-mapeditor'
    }
    jpackage {
        // not sure if these are properly passed to jpackage call
        installerOptions = [
                '--description', 'JavaFX 3D',
                '--copyright', '2021-2024',
                '--vendor', 'Armin Reichert'
        ]
        if (os.isWindows()) {
            installerType = 'exe'
            installerOptions.addAll(['--win-dir-chooser', '--win-menu', '--win-menu-group', 'Pac-Man FX', '--win-shortcut'])
        }
    }
}

jlinkZip {
    group = 'distribution'
}
