plugins {
    id 'java'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.jlink' version '3.0.1' //  adds also 'application' plugin
}

java.sourceCompatibility = JavaVersion.VERSION_17

application {
    mainModule = 'de.amr.games.pacman.ui3d'
    mainClass = 'de.amr.games.pacman.ui3d.Main'
    if (os.isLinux()) {
        applicationDefaultJvmArgs = ['-Dprism.forceGPU=true']
    }
}

dependencies {
    implementation project(':pacman-lib')
    implementation project(':pacman-core')
    implementation project(':pacman-ui-2d')
    implementation project(':pacman-mapeditor')
    implementation 'org.tinylog:tinylog-api:2.7.0'
    implementation 'org.tinylog:tinylog-impl:2.7.0'
}

javafx {
    version = '21'
    modules = ['javafx.base', 'javafx.graphics', 'javafx.controls', 'javafx.media']
}

jlink {
    imageZip = layout.buildDirectory.file("distributions/javafx-ui3d-${javafx.platform.classifier}.zip")
    options = ['--compress', 'zip-9', '--strip-debug', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'Pac-Man-FX-3D'
    }
    jpackage {
        installerOptions = ['--vendor', 'Armin Reichert', '--app-version', '1.0']
        if (os.isWindows()) {
            icon = 'pacman.ico'
            installerType = 'msi'
            installerOptions += ['--win-dir-chooser', '--win-menu', '--win-menu-group', 'Pac-Man FX', '--win-shortcut']
        }
        if (os.isLinux()) {
            installerType='deb'
        }
    }
}

jlinkZip {
    group = 'distribution'
}