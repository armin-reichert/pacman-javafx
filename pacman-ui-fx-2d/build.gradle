plugins {
    id 'java'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.jlink' version '3.0.1' //  adds also 'application' plugin
}

application {
    mainModule = 'mod.pacman_ui_fx_2d'
    mainClass = 'de.amr.games.pacman.ui.fx.Main'
}

dependencies {
    implementation 'org.tinylog:tinylog-api:2.7.0'
    implementation 'org.tinylog:tinylog-impl:2.7.0'
    testImplementation 'junit:junit:4.13.2'
    implementation project(':pacman-lib')
    implementation project(':pacman-core')
}

javafx {
    version = '21.0.2'
    modules = ['javafx.base', 'javafx.graphics', 'javafx.controls', 'javafx.media']
}

jlink {
    imageZip = project.file("${layout.buildDirectory}/distributions/app-${javafx.platform.classifier}.zip")
    options = [
            '--compress', 'zip-9',
            '--strip-debug',
            '--no-header-files',
            '--no-man-pages']
    launcher {
        name = 'Pac-Man-FX-2D'
    }
    jpackage {
        appVersion = '1.0'
        installerOptions = [
            '--vendor', 'Armin Reichert',
        ]
        if (os.isWindows()) {
            icon = 'pacman.ico'
            installerType = 'msi'
            installerOptions.addAll([
                    '--win-dir-chooser',
                    '--win-menu',
                    '--win-menu-group', 'Pac-Man FX',
                    '--win-shortcut'])
        }
    }
}

jlinkZip {
    group = 'distribution'
}
