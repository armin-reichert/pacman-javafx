plugins {
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.jlink' version '3.0.1' //  adds also 'application' plugin
}

application {
    mainModule = 'mod.pacman_ui_fx_3d'
    mainClass = 'de.amr.games.pacman.ui.fx.v3d.Main'
    if (os.isLinux()) {
        applicationDefaultJvmArgs = ['-Dprism.forceGPU=true']
    }
}

dependencies {
    implementation project(':pacman-lib')
    implementation project(':pacman-core')
    implementation project(':pacman-ui-fx-2d')
    implementation project(':tilemap-editor')
}

javafx {
    version = '21.0.2'
    modules = ['javafx.base', 'javafx.graphics', 'javafx.controls', 'javafx.media']
}

jlink {
    imageZip = project.file("${layout.buildDirectory}/distributions/app-${javafx.platform.classifier}.zip")
    options = [
        '--strip-debug',
        '--compress', 'zip-9',
        '--no-header-files',
        '--no-man-pages'
    ]
    launcher {
        name = 'pacman-javafx-3d'
    }
    jpackage {
        appVersion = '1.0'
        installerOptions.addAll([
//            '--copyright', 'COPYRIGHT_HERE',
//            '--description', 'DESCRIPTION_HERE',
//            '--help', 'HELP_ME',
//            '--name', 'Name_HERE',
            '--vendor', 'Vendor_HERE',
//            '--version', 'Version_HERE'
        ])
        if (os.isWindows()) {
            icon = 'pacman.ico'
            installerType = 'msi'
            installerOptions.addAll([
                '--win-dir-chooser',
                '--win-menu',
                '--win-menu-group', 'Pac-Man FX',
                '--win-shortcut'])
        }
    }
}

jlinkZip {
    group = 'distribution'
}