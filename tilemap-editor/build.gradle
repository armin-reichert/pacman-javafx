plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.jlink' version '3.0.1'
}

application {
    mainModule = 'mod.tilemapeditor'
    mainClass = 'de.amr.games.pacman.tilemapeditor.Main'
}

dependencies {
    implementation project(':pacman-lib')
    implementation project(':pacman-core')
    implementation project(':pacman-ui-fx-2d') //TODO should not depend on complete project
    implementation 'org.tinylog:tinylog-api:2.7.0'
    implementation 'org.tinylog:tinylog-impl:2.7.0'
}

javafx {
    version = '21.0.2'
    modules = ['javafx.base', 'javafx.graphics', 'javafx.controls']
}

jlink {
    imageZip = project.file("${layout.buildDirectory}/distributions/app-${javafx.platform.classifier}.zip")
    options = [
            '--strip-debug',
            '--compress', 'zip-9',
            '--no-header-files',
            '--no-man-pages']
    launcher {
        name = 'Pac-Man-FX-Map-Editor'
    }
    jpackage {
        appVersion = '1.0'
        vendor = 'Armin Reichert'
        installerOptions = [
                '--vendor', 'Armin Reichert',
        ]
        if (os.isWindows()) {
            icon = 'pacman.ico'
            installerType = 'msi'
            installerOptions.addAll([
                    '--win-dir-chooser',
                    '--win-menu',
                    '--win-menu-group', 'Pac-Man FX',
                    '--win-shortcut'])
        }
    }
}

jlinkZip {
    group = 'distribution'
}