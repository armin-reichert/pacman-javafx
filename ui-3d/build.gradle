plugins {
    id 'java'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.jlink' version '3.0.1' //  adds also 'application' plugin
}

application {
    mainModule = 'de.amr.games.pacman.ui3d'
    mainClass = 'de.amr.games.pacman.ui3d.Main'
    if (os.isLinux()) {
        applicationDefaultJvmArgs = ['-Dprism.forceGPU=true']
    }
}

dependencies {
    implementation project(':lib')
    implementation project(':core')
    implementation project(':ui-2d')
    implementation project(':mapeditor')
    implementation 'org.tinylog:tinylog-api:2.7.0'
    implementation 'org.tinylog:tinylog-impl:2.7.0'
}

javafx {
    version = '21.0.2'
    modules = ['javafx.base', 'javafx.graphics', 'javafx.controls', 'javafx.media']
}

jlink {
    imageZip = project.file("${layout.buildDirectory}/distributions/app-${javafx.platform.classifier}.zip")
    options = [
        '--compress', 'zip-9',
        '--strip-debug',
        '--no-header-files',
        '--no-man-pages'
    ]
    launcher {
        name = 'Pac-Man-FX-3D'
    }
    jpackage {
        appVersion = '1.0'
        // not supported: copyright
        description = 'Pac-Man JavaFX 3D'
        icon = 'pacman.ico'
        vendor = 'Armin Reichert'
        installerOptions = [
            '--copyright', '2021-2024 Armin Reichert',
        ]
        if (os.isWindows()) {
            icon = 'pacman.ico'
            installerType = 'msi'
            installerOptions.addAll([
                '--win-dir-chooser',
                '--win-menu',
                '--win-menu-group', 'Pac-Man FX',
                '--win-shortcut'])
        }
    }
}

jlinkZip {
    group = 'distribution'
}